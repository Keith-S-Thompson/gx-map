#!%PERL%

# $Id: gx-install-cadesc.in,v 1.18 2007-09-18 20:23:54-07 kst Exp $
# $Source: /home/kst/gx-map-redacted/gx-install-cadesc.in,v $

########################################################################
# @Copyright@
#
# Copyright (c) 2007 The Regents of the University of California. All
# rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are
# met:
#
# 1. Redistributions of source code must retain the above copyright
# notice, this list of conditions and the following disclaimer.
#
# 2. Redistributions in binary form must reproduce the above copyright
# notice, this list of conditions and the following disclaimer in the
# documentation and/or other materials provided with the distribution.
#
# 3. All advertising materials mentioning features or use of this
# software must display the following acknowledgement: This product
# includes software developed by the Advanced Cyberinfrastructure
# Laboratory at the San Diego Supercomputer Center and its contributors.
#
# 4. Neither the name of the Center nor the names of its contributors
# may be used to endorse or promote products derived from this software
# without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE REGENTS AND CONTRIBUTORS ``AS IS''
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO,
# THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR
# PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE REGENTS OR CONTRIBUTORS
# BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
# CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
# SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
# BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
# WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE
# OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN
# IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
#
# @Copyright@
########################################################################

########################################################################
# Developed by Keith Thompson <kst@sdsc.edu>
########################################################################

use strict;
use warnings;

use File::Basename ();
use Getopt::Long ();

sub Usage(@);
sub Expire_Files($);
sub Backup($);
sub Debug(@);
sub Debugf(@);
use Cwd ();

# 
# Make warnings fatal.
# 
$SIG{__WARN__} = sub { die @_ };

my $Install_Dir;
BEGIN { $Install_Dir = '%INSTALL_DIR%'; }
use lib "$Install_Dir/lib";
use GX ();

my $Program_Name = File::Basename::basename $0;

my $User_Opt = { 'install-dir' => $Install_Dir };
my @Opts = ( $User_Opt,
             qw( help
                 version
                 install-dir=s
                 name=s
                 primary!
                 debugging! ) );
Getopt::Long::GetOptions @Opts or Usage;
Usage if $User_Opt->{help};
if ($User_Opt->{version}) {
    print "$Program_Name (gx-map) $GX::VERSION\n";
    exit 0;
}
Usage if scalar @ARGV != 1;

GX::Set_Config '-ignore-errors';

if (not defined $User_Opt->{name} and not defined $User_Opt->{primary}) {
    Usage qq(Either "-name" or "-primary" must be specified\n);
}
elsif (defined $User_Opt->{name} and defined $User_Opt->{primary}) {
    Usage qq(The "-name" and "-primary" options may not both be specified\n);
}

my $Config_Dir;
if (defined $User_Opt->{primary}) {
    $Config_Dir = 'ca-config';
}
else {
    if ($User_Opt->{name} =~ /^[\w.-_]+$/) {
        $Config_Dir = "$User_Opt->{name}.ca-config";
    }
    else {
        Usage qq(Bad value for -name option: "$User_Opt->{name}"\n);
    }
}

my $tarball = $ARGV[0];

if ($tarball !~ /\.tar\.gz$/) {
    Usage "Invalid file name: $tarball\n";
}

if ($tarball !~ /^\//) {
    my $cwd = Cwd::cwd();
    $tarball = "$cwd/$tarball";
}

GX::Install_Die_Handler;
GX::Install_Warn_Handler;

GX::Use_Commands 'gzip', 'tar';

my $dir = File::Basename::basename $tarball;
$dir =~ s/\.tar\.gz$//;

$| = 1;

chdir "$User_Opt->{'install-dir'}/etc/gx-map"
    or die "$User_Opt->{'install-dir'}: $!\n";

my $backup_ca_config_dir = undef;
if (-d $Config_Dir) {
    $backup_ca_config_dir = Backup $Config_Dir;
}

print "% gzip -d < $tarball | tar xf -\n";
my $result = system "gzip -d < $tarball | tar xf -";
die "Command failed\n" if $result != 0;
die "Directory $dir not created\n" if not -d $dir;

Expire_Files $dir;

rename $dir, $Config_Dir or die "Renaming $dir to $Config_Dir: $!\n";

if (defined $backup_ca_config_dir) {
    print <<"EOF";
Your existing directory
    $User_Opt->{'install-dir'}/etc/gx-map/$Config_Dir
has been renamed to
    $User_Opt->{'install-dir'}/etc/gx-map/$backup_ca_config_dir
and a new directory has been created and populated in its place.

If you've created or customized any *.cadesc files, you can copy them
from the backup directory to the new $Config_Dir directory.
If not, you can remove the backup directory.
EOF
}

########################################################################

# 
# Given a time, return an ASCII UTC timestamp suitable for use
# in a file name, such as " 2006-09-15-022924Z".
#   
sub Timestamp_Of($) {
    my($time) = @_;
    my ($sec, $min, $hour, $mday, $mon, $year, $wday, $yday, $isdst) =  
        gmtime($time);
    return sprintf "%04d-%02d-%02d-%02d%02d%02dZ",
                   $year + 1900, $mon + 1, $mday, $hour, $min, $sec;
} # Timestamp_Of
    
# ----------------------------------------------------------------------

#
# In the specified directory, for each *.cadesc file (other than
# 00000000.example.casdesc), move the file into an EXPIRED subdirectory
# if the CA certificate has expired.  Then move any disabled cadesc
# files into a DISABLED directory.
#
sub Expire_Files($) {
    my($dir) = @_;
    
    my $Now = time;

    print "% mkdir $dir/EXPIRED\n";
    mkdir "$dir/EXPIRED" or die "mkdir $dir/EXPIRED: $!\n";

    print "% mkdir $dir/DISABLED\n";
    mkdir "$dir/DISABLED" or die "mkdir $dir/DISABLED: $!\n";

    opendir my $DIR, $dir or die "${dir}: $!\n";
    my @cadesc_files = sort grep /\.cadesc$/, readdir $DIR;
    closedir $DIR;
    FILE:
    foreach my $file (@cadesc_files) {
        next FILE if $file =~ /^00000000/;
        my $ref = GX::Read_Records format   => 'single',
                                   filename => "$dir/$file";
        if (defined $ref->{CERT_EXPIRES}) {
            my $expires = GX::Time_Value $ref->{CERT_EXPIRES};
            if (defined $expires and $expires < $Now) {
                rename "$dir/$file", "$dir/EXPIRED/$file";
                next FILE;
            }
        }
        if (defined $ref->{DISABLE} and $ref->{DISABLE} eq 'yes') {
            rename "$dir/$file", "$dir/DISABLED/$file";
        }
    }
} # Expire_Files

# ----------------------------------------------------------------------
        
#   
# Rename the specified file by appending its last modification time
# plus an index number (to ensure uniqueness) to its name, returning
# the name of the backup (which may be ignored if it's not needed).
# If file doesn't exist, return undef.  May be used on directories as
# well as ordinary files.
#
sub Backup($) {
    my($filename) = @_;
    my @stat;   
    
    return undef if not -e $filename;
    my $timestamp = Timestamp_Of GX::Get_mtime $filename;
    my $index = 0;
    my $backup_name;
    do {
        $backup_name = sprintf "%s-%s-%03d", $filename, $timestamp, $index++;
    } while -e $backup_name;
    rename $filename, $backup_name or die "$backup_name: $!\n";
    return $backup_name;
} # Backup

# ----------------------------------------------------------------------

sub Usage(@) {
    print @_ if @_;
    print <<"EOF";
Usage: $Program_Name [options] filename
The file must have a name ending in ".tar.gz".
Option names may be abbreviated.
    -help             Display this message and exit
    -version          Display version information and exit
    -install-dir DIR  gx-map installation directory
                      Default is $Install_Dir
    -name NAME        Install directory as "NAME.ca-config"
    -primary          Install directory as "ca-config"
                      Either "-name" or "-primary" must be given
    -debugging        Display debugging messages
EOF
    exit 1;
} # Usage

# ----------------------------------------------------------------------

sub Debug(@) {
    print @_ if $User_Opt->{debugging};
} # Debug

# ----------------------------------------------------------------------

sub Debugf(@) {
    printf @_ if $User_Opt->{debugging};
} # Debugf
