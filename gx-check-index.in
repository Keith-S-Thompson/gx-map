#!%PERL%

# $Id: gx-check-index.in,v 1.14 2007-02-08 17:32:59-08 kst Exp $
# $Source: /home/kst/gx-map-redacted/gx-check-index.in,v $

########################################################################
# @Copyright@
#
# Copyright (c) 2007 The Regents of the University of California. All
# rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are
# met:
#
# 1. Redistributions of source code must retain the above copyright
# notice, this list of conditions and the following disclaimer.
#
# 2. Redistributions in binary form must reproduce the above copyright
# notice, this list of conditions and the following disclaimer in the
# documentation and/or other materials provided with the distribution.
#
# 3. All advertising materials mentioning features or use of this
# software must display the following acknowledgement: This product
# includes software developed by the Advanced Cyberinfrastructure
# Laboratory at the San Diego Supercomputer Center and its contributors.
#
# 4. Neither the name of the Center nor the names of its contributors
# may be used to endorse or promote products derived from this software
# without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE REGENTS AND CONTRIBUTORS ``AS IS''
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO,
# THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR
# PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE REGENTS OR CONTRIBUTORS
# BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
# CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
# SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
# BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
# WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE
# OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN
# IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
#
# @Copyright@
########################################################################

########################################################################
# Developed by Keith Thompson <kst@sdsc.edu>
########################################################################

use strict;
use warnings;

use File::Basename ();
use File::Copy ();
use Getopt::Long ();

my $Install_Dir;
BEGIN {
    $Install_Dir = '%INSTALL_DIR%';
    unshift @INC, "$Install_Dir/lib";
}
use GX ();

sub Usage(@);
sub Debug(@);
sub Debugf(@);
sub Get_CA_Info($);
sub Get_Index_Info($);  # filename -> hash ref
sub Request($$$);       # add|remove, ref, comment; called by Gen_Request
sub Gen_Request($$);    # old_ref new_ref

my $gx_request_command = "$Install_Dir/bin/gx-request";

my $Program_Name = File::Basename::basename $0;
my $User_Opt = {};
my @Opts = ( $User_Opt, qw( help!
                            version!
                            ca=s
                            index=s
                            dryrun!
                            debugging! ) );
Getopt::Long::GetOptions @Opts or Usage;
Usage if $User_Opt->{help};
if ($User_Opt->{version}) {
    print "$Program_Name (gx-map) $GX::VERSION\n";
    exit 0;
}
Usage if @ARGV;
if (not defined $User_Opt->{ca} or not defined $User_Opt->{index}) {
    Usage "Both \"-ca\" and \"-index\" must be specified\n";
}

GX::Install_Die_Handler;
GX::Install_Warn_Handler;

my $CA = Get_CA_Info $User_Opt->{ca};
if (ref $CA eq 'ARRAY') {
    #
    # The returned value is an error message, not a hash reference
    #
    my @errors = map "$_\n", @$CA;
    die @errors;
}

#
# $User_Opt->{index} may be either a file name or a URL.
# $Index_File is a file name (possibly a cache file).
#
my $Index_File;
my $Index_Is_Cached = 0;
if (GX::Is_URL $User_Opt->{index}) {
    my $result = GX::Cache_File $User_Opt->{index};
    if (defined $result->{filename}) {
        $Index_File = $result->{filename};
        $Index_Is_Cached = 1;
    }
    else {
        die "$User_Opt->{index}: $result->{error}\n";
    }
}
else {
    $Index_File = $User_Opt->{index};
}

my $Data_Dir = "$Install_Dir/gx-map-data/ca-index";

GX::Get_Lock $Program_Name;

my $Saved_File = "$Data_Dir/$CA->{HASH}.index";
my $Error_File = "$Data_Dir/$CA->{HASH}.error";
my $New_File   = "$Saved_File.new";

if (not -e $Saved_File) {
    #
    # We don't have a saved copy of ca.db.index.
    # Create it as an empty file and check it into RCS.
    #
    open FILE, ">$Saved_File" or die "$Saved_File: $!\n";
    close FILE;
    system "ci -l $Saved_File </dev/null >/dev/null 2>/dev/null";
}

#
# $Index_File is the system ca.db.index file.
#
# $Saved_File is a saved copy of the index file, presumably created by
# an earlier execution of this program.
#
# $New_File is a temporary copy of $Index_File, to be compared to
# $Saved_File.
#

my @index_stat = stat $Index_File;
my @saved_stat = stat $Saved_File;
my $Index_File_Size  = $index_stat[7];
my $Index_File_Mtime = $index_stat[9];
my $Saved_File_Size  = $saved_stat[7];
my $Saved_File_Mtime = $saved_stat[9];

if ($Saved_File_Size == $Index_File_Size and
    $Saved_File_Mtime == $Index_File_Mtime)
{
    Debug "Index file has not been not updated, bailing out\n";
    GX::Release_Lock $Program_Name;
    exit 0;
}

my $result = system 'cp', '-pf', $Index_File, $New_File;
die "cp failed\n" if $result != 0;

my $old_info = Get_Index_Info $Saved_File;
my $new_info = Get_Index_Info $New_File;

my @old_serials = sort { hex $a <=> hex $b } keys %$old_info;
my @new_serials = sort { hex $a <=> hex $b } keys %$new_info;

#
# Confirm that all the serial numbers in the old index file are still
# in the new index file.
# Confirm that all DNs in the old index file are unchanged in the
# new index file.
#
Debug "Checking for errors\n";
my @missing_serials = ();
my @changed_DNs = ();
foreach my $serial (@old_serials) {
    if (not defined $new_info->{$serial}) {
        push @missing_serials, $serial;
    }
    elsif ($new_info->{$serial}->{dn} ne $old_info->{$serial}->{dn}) {
        push @changed_DNs, $serial;
    }
}
if (@missing_serials or @changed_DNs) {
    my $mail_program = undef;
    foreach my $cmd ('/bin/mailx', '/bin/mail') {
        if (-x $cmd) {
            $mail_program = $cmd;
            last;
        }
    }

    my $message = '';
    if (@missing_serials) {
        $message .= "Missing serial numbers: @missing_serials\n";
    }
    if (@changed_DNs) {
        $message .= "Changed DNs: @changed_DNs\n";
    }

    open my $ERROR, '>', $Error_File;
    print $ERROR $message;
    close $ERROR;

    GX::Send_Email $Program_Name,
                   $GX::Config{ADMIN_EMAIL},
                   "$User_Opt->{ca} problem",
                   $message;

    File::Copy::copy $Saved_File, "$New_File.bad";
    # GX::Release_Lock $Program_Name;
    exit 1;
}

foreach my $serial (@new_serials) {
    Debug "Checking cert $serial";
    my $old_ref = $old_info->{$serial}; # possibly undef
    my $new_ref = $new_info->{$serial};

    if (not defined $new_ref->{uid}) {
        Debug ", not a user certificate\n";
        next;
    }
    if (not defined $old_ref) {
        #
        # New entry
        #
        Debug ", new entry, $new_ref->{status}\n";
        Gen_Request undef, $new_ref;
    }
    elsif ($old_ref->{line} ne $new_ref->{line}) {
        #
        # Modified entry
        #
        Debug ", modified entry, $old_ref->{status}", '->',
                                 "$new_ref->{status}\n";
        Gen_Request $old_ref, $new_ref;
    }
    else {
        Debug ", unchanged\n";
    }
}

if ($User_Opt->{dryrun}) {
    print "% rename $New_File $Saved_File\n";
    print "% chdir $Data_Dir\n";
    print "% ci -l $Saved_File\n";
    unlink $New_File;
}
else {
    rename $New_File, $Saved_File;
    chdir $Data_Dir;
    GX::Use_Commands 'ci';
    system "ci -l $Saved_File </dev/null >/dev/null 2>/dev/null";
}

GX::Release_Lock $Program_Name;

########################################################################

#
# Given either a hexadecimal hash or a short name, search for
# a matching *.cadesc file and return information about the CA
# using GX::Get_CA_Ref.
#
sub Get_CA_Info($) {
    my($name) = @_;
    my $is_hash = $name =~ /^[\da-f]{8}$/;
    my $ca_dir = "$Install_Dir/etc/gx-map/ca-config";

    opendir my $DIR, $ca_dir or die "$ca_dir: $!\n";
    my @files = grep /\.cadesc$/, readdir $DIR;
    closedir $DIR;

    my @matches = ();
    my $matching_filename;
    foreach my $file (@files) {
        my($hash, $short_name, $suffix) = split /\./, $file;
        next if not defined $suffix or $suffix ne 'cadesc';
        next if $hash eq '00000000';
        if ($is_hash and $hash eq $name) {
            push @matches, $name;
            $matching_filename = $file;
        }
        elsif (not $is_hash and lc $short_name eq $name) {
            push @matches, $name;
            $matching_filename = $file;
        }
    }
    if (scalar @matches == 0) {
        die "No matches for CA name $name\n";
    }
    elsif (scalar @matches > 1) {
        die "Multiple matches for CA name ${name}: @matches\n";
    }

    return GX::Get_CA_Ref "$ca_dir/$matching_filename";
} # Get_CA_Info

# ----------------------------------------------------------------------

#
# Returns a reference to a hash, keyed by serial number, of
# entry references.
# Each entry reference is a reference to a hash of the information
# extracted from the entry.
#
sub Get_Index_Info($) {
    my($filename) = @_;
    my $info = {};

    open FILE, $filename or die "${filename}: $!\n";
    while (<FILE>) {
        chomp;
        next LINE if /^$/;
        my $ref = {};
        my @fields = split /\t/, $_;
        if (scalar @fields != 6) {
            die "$filename, ", scalar @fields, " fields on line $.\n"
        }
        $ref->{line} = $_;
        $ref->{status}   = $fields[0];
        $ref->{expires}  = $fields[1];
        $ref->{revoked}  = $fields[2];
        $ref->{serial}   = $fields[3];
        $ref->{unknown}  = $fields[4];
        $ref->{dn}       = GX::Normalize_DN $fields[5];
        $ref->{is_valid} = $ref->{status} eq 'V';

        if ($ref->{dn} =~ m[/UID=([^/]+)]) {
            $ref->{uid} = $1;
        }
        if ($ref->{dn} =~ m[/emailAddress=([^/]+)]) {
            $ref->{email} = $1;
        }
        $info->{$ref->{serial}} = $ref;
    }
    return $info;
} # Get_Index_Info

# ----------------------------------------------------------------------

sub Request($$$) {
    my ($action, $ref, $comment) = @_;
    my @command = ($gx_request_command);

    if    ($action eq 'add')    { push @command, '-add' }
    elsif ($action eq 'remove') { push @command, '-remove' }
    else                        { die "Internal error, action=$action\n" }
    push @command, '-quiet';
    push @command, '-force';
    push @command, '-dn', $ref->{dn};
    push @command, '-username', $ref->{uid};
    if (defined $ref->{email}) {
        push @command, '-email', $ref->{email};
    }

    push @command, '-source', "CA_$CA->{HASH}";

    push @command, '-comment',
                   "$CA->{HASH} $CA->{CA_NAME} # $ref->{serial} ($comment)";

    if ($User_Opt->{dryrun}) {
        my @printable_command = @command;
        foreach (@printable_command) {
            $_ = "\"$_\"" if $_ =~ / /;
        }
        print "% @printable_command\n";
    }
    else {
        system @command;
    }
} # Request

# ----------------------------------------------------------------------

sub Gen_Request($$) {
    my($old_ref, $new_ref) = @_;

    if (defined $old_ref) {
        #
        # If the old and new refs are both valid or both invalid,
        # we don't need to request and update, since the DN and username
        # are guaranteed to be unchanged (we already checked that)
        #
        my $status_change
            = "$old_ref->{status}" . '->' . "$new_ref->{status}";

        if ($old_ref->{is_valid} and not $new_ref->{is_valid}) {
            Request 'remove', $new_ref, $status_change;
        }
        elsif (not $old_ref->{is_valid} and $new_ref->{is_valid}) {
            Request 'add', $new_ref, $status_change;
        }
    }
    else {
        #
        # If the new ref is invalid, don't do anything (perhaps
        # a cert was issued and revoked between checks).
        #
        if ($new_ref->{is_valid}) {
            Request 'add', $new_ref, $new_ref->{status};
        }
    }
} # Gen_Request

# ----------------------------------------------------------------------

sub Debug(@) {
    print @_ if $User_Opt->{debugging};
} # Debug

# ----------------------------------------------------------------------

sub Debugf(@) {
    printf @_ if $User_Opt->{debugging};
} # Debugf

# ----------------------------------------------------------------------

sub Usage(@) {
    print @_ if @_;
    print <<"EOF";
Usage: $Program_Name [options]
The index-file argument can be either a local file name or a URL.
    -help         : Display this message and exit
    -version      : Display version number and exit
    -ca name      : The CA being handled.  Mandatory.
                    The CA name can be given either as a hexadecimal
                    hash value (such as "3deda549" or "b89793e4") or as
                    a short name (such as "sdsc" or "npaci")
    -index index  : Index file.  Mandatory.
                    This can be either a local file name or a URL.
    -dryrun       : Don't perform updates
    -debugging    : Enable debugging output
EOF
    exit 1;
} # Usage
