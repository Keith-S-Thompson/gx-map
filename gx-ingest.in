#!%PERL% -w

# $Id: gx-ingest.in,v 1.15 2005-07-21 17:11:57-07 kst Exp $
# $Source: /home/kst/gx-map-redacted/gx-ingest.in,v $

########################################################################
# @Copyright@
#
# Copyright (c) 2005 The Regents of the University of California. All
# rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are
# met:
#
# 1. Redistributions of source code must retain the above copyright
# notice, this list of conditions and the following disclaimer.
#
# 2. Redistributions in binary form must reproduce the above copyright
# notice, this list of conditions and the following disclaimer in the
# documentation and/or other materials provided with the distribution.
#
# 3. All advertising materials mentioning features or use of this
# software must display the following acknowledgement: This product
# includes software developed by the Grid and Cluster Computing Group
# at the San Diego Supercomputer Center and its contributors.
#
# 4. Neither the name of the Center nor the names of its contributors
# may be used to endorse or promote products derived from this software
# without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE REGENTS AND CONTRIBUTORS ``AS IS''
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO,
# THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR
# PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE REGENTS OR CONTRIBUTORS
# BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
# CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
# SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
# BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
# WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE
# OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN
# IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
#
# @Copyright@
########################################################################

########################################################################
# Developed by Keith Thompson <kst@sdsc.edu>
########################################################################

use strict;

use File::Basename ();
use Getopt::Long ();

my $Install_Dir;
BEGIN {
    $Install_Dir = '%INSTALL_DIR%';
    unshift @INC, "$Install_Dir/lib";
}
use Gridmap_Utils ();

sub Usage(@);

my $Program_Name = File::Basename::basename $0;

my $User_Opt = {};
my @Opts = ( $User_Opt,
             qw( help
                 version
                 old! ) );
Getopt::Long::GetOptions @Opts or Usage;
Usage if $User_Opt->{help};

if ($User_Opt->{version}) {
    print "$Program_Name (gx-map) $Gridmap_Utils::VERSION\n";
    exit 0;
}

my $gx_request_command = "$Install_Dir/bin/gx-map";
if (-x "$Install_Dir/bin/gx-request") {
    $gx_request_command = "$Install_Dir/bin/gx-request";
}

print <<"EOF";
#!/bin/sh

#
# This script was generated by gx-ingest (gx-map version $Gridmap_Utils::VERSION)
# Please check it carefully before running it.
#

gx_request=$gx_request_command  # Customize this if necessary.

EOF

my %seen = ();

while (<>) {
    chomp;
    s/#.*$//;
    s/^\s+//;
    s/\s+$//;
    next if /^$/;
    my($dn, $users) = /^\s*("[^"]*")\s*(\S+)\s*$/;
    if (not defined $dn or not defined $users) {
        print "# >>> Can't parse line $.: $_\n";
    }
    else {
        #
        # Normally, only one user is specified, but it's also possible
        # to have a list of user name separated by commas.  In this case,
        # add them in reverse order; the most recently added is treated
        # as the default.
        #
        # We avoid duplicate mappings (common if the input grid-mapfile
        # is compatible with both GT2 and GT3) using the %seen hash.
        #
        $dn = Gridmap_Utils::GT3_Subject $dn;
        my @users = reverse split /,/, $users;
        foreach my $user (@users) {
            next if $seen{"$dn $user"}++;
            my $command = "\$gx_request -add -force ";
            $command .= "-no-email" if $User_Opt->{old};
            $command .= "-comment 'Mapping generated by $Program_Name' ";
            $command .= sprintf "-username %-8s ", $user;
            $command .= "-dn $dn";
            print "$command\n";
        }
    }
}

########################################################################

sub Usage(@) {
    print @_ if @_;
    print <<EOF;
Usage: $Program_Name [options] [files]
    -help              : Display this message and exit.
    -version           : Display version information and exit.
    -old               : Use the obsolete "-no-email" option.
                         This is needed only for gx-map 0.3 and earlier.

Reads an existing grid-mapfile (from stdin if no name is specified).

Writes on stdout a /bin/sh script that invokes gx-map for each entry
in the input grid-mapfile.  The generated script must be run by a
privileged user (see GLOBUS_ADMINS in your gx-map config file).

Be sure to check the generated script carefully before running it.
EOF
    exit 1;
}
