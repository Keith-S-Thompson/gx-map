#!%PERL% -w

# $Id: gx-ingest.in,v 1.7 2005-01-07 16:55:55-08 kst Exp $
# $Source: /home/kst/gx-map-redacted/gx-ingest.in,v $

########################################################################
# @Copyright@
#
# Copyright (c) 2005 The Regents of the University of California. All
# rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are
# met:
#
# 1. Redistributions of source code must retain the above copyright
# notice, this list of conditions and the following disclaimer.
#
# 2. Redistributions in binary form must reproduce the above copyright
# notice, this list of conditions and the following disclaimer in the
# documentation and/or other materials provided with the distribution.
#
# 3. All advertising materials mentioning features or use of this
# software must display the following acknowledgement: This product
# includes software developed by the Grid and Cluster Computing Group
# at the San Diego Supercomputer Center and its contributors.
#
# 4. Neither the name of the Center nor the names of its contributors
# may be used to endorse or promote products derived from this software
# without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE REGENTS AND CONTRIBUTORS ``AS IS''
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO,
# THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR
# PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE REGENTS OR CONTRIBUTORS
# BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
# CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
# SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
# BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
# WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE
# OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN
# IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
#
# @Copyright@
########################################################################

########################################################################
# Developed by Keith Thompson <kst@sdsc.edu>
########################################################################

use strict;

use File::Basename ();

sub Usage(@);

my $Program_Name = File::Basename::basename $0;

#
# Temporary kludge to invoke usage message: if the first
# command-line argument starts with '-' or is a nonexistent file,
# call Usage.
#
# To do: implement a proper "-help" option with Getopt::Long.
#
if (@ARGV) {
    Usage if $ARGV[0] =~ /^-/;
    Usage if not -r $ARGV[0];
}

my $gxmap_command = undef;
DIR:
foreach my $dir (split /:/, $ENV{PATH}) {
    if (-x "$dir/gx-map") {
        $gxmap_command = "$dir/gx-map";
        last DIR;
    }
}

my $definition;
if (defined $gxmap_command) {
    $definition = "gxmap=$gxmap_command  # Customize this if necessary";
}
else {
    $definition = 'gxmap=/path/to/gx-map  # Customize this';
}

print <<"EOF";
#!/bin/sh

#
# This script was generated by $Program_Name.
# Please check it carefully before running it.
#

$definition

EOF

while (<>) {
    chomp;
    s/#.*$//;
    s/^\s+//;
    s/\s+$//;
    next if /^$/;
    my($DN, $user) = /^\s*("[^"]*")\s*(\S+)\s*$/;
    if (not defined $DN or not defined $user) {
        print "# >>> Can't parse line $.: $_\n";
    }
    else {
        my $user8 = sprintf "%-8s", $user;
        print "\$gxmap -add -force -no-email ",
              "-comment 'Mapping generated by $Program_Name' ",
              " -username $user8 -dn $DN\n";
    }
}

########################################################################

sub Usage(@) {
    print @_ if @_;
    print <<EOF;
Usage: $Program_Name [files]

Reads an existing grid-mapfile (from stdin if no name is specified).

Writes on stdout a /bin/sh script that invokes gx-map for each entry
in the input grid-mapfile.  The generated script must be run by a
privileged user (see GLOBUS_ADMINS in your gx-map config file).

Be sure to check the generated script carefully before running it.
EOF
    exit 1;
}
