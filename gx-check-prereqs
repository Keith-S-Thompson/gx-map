#!/usr/bin/perl -w

# $Id: gx-check-prereqs,v 1.25 2007-01-01 13:01:33-08 kst Exp $
# $Source: /home/kst/gx-map-redacted/gx-check-prereqs,v $

########################################################################
# @Copyright@
#
# Copyright (c) 2007 The Regents of the University of California. All
# rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are
# met:
#
# 1. Redistributions of source code must retain the above copyright
# notice, this list of conditions and the following disclaimer.
#
# 2. Redistributions in binary form must reproduce the above copyright
# notice, this list of conditions and the following disclaimer in the
# documentation and/or other materials provided with the distribution.
#
# 3. All advertising materials mentioning features or use of this
# software must display the following acknowledgement: This product
# includes software developed by the Advanced Cyberinfrastructure
# Laboratory at the San Diego Supercomputer Center and its contributors.
#
# 4. Neither the name of the Center nor the names of its contributors
# may be used to endorse or promote products derived from this software
# without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE REGENTS AND CONTRIBUTORS ``AS IS''
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO,
# THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR
# PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE REGENTS OR CONTRIBUTORS
# BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
# CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
# SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
# BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
# WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE
# OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN
# IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
#
# @Copyright@
########################################################################

########################################################################
# Developed by Keith Thompson <kst@sdsc.edu>
########################################################################

use strict;

use Getopt::Long ();
use File::Basename ();

sub Usage(@);
sub Check_Perl($);
sub Check_Command($);
sub Check_Module(@);
sub Vprint(@);
sub Vprintf(@);

$| = 1;

my $Program_Name = File::Basename::basename $0;

my %Opt = ( path => '/bin:/usr/bin' );
my @Opts = ( \%Opt, qw( help
                        version
                        path=s
                        verbose!
                        teragrid! ) );
Getopt::Long::GetOptions @Opts or Usage;
Usage if $Opt{help};
Usage if @ARGV;

if ($Opt{version}) {
    print "$Program_Name (gx-map) (no version)\n";
    exit 1;
}

$ENV{PATH} = $Opt{path};
my @path = split /:/, $ENV{PATH};

my @path_errors = ();
foreach my $dir (@path) {
    if (not -d $dir) {
        push @path_errors, "No directory $dir\n";
    }
    elsif ($dir !~ /^\//) {
        push @path_errors, "Directory $dir is not an absolute pathname\n";
    }
}
die @path_errors if @path_errors;

my @Errors = ();
my @Warnings = ();
my $Perl_OK = 1;
my $All_Commands_Found = 1;
my $All_Modules_Found = 1;

Check_Perl 5.006;

Check_Command 'curl';
Check_Command 'rcs';
Check_Command 'ci';
Check_Command 'openssl';
Check_Command 'gzip';
Check_Command 'tar';

Check_Module 'File::Basename';
Check_Module 'File::Copy';
Check_Module 'Getopt::Long';
Check_Module 'Time::Local';
Check_Module 'Cwd';
Check_Module 'Net::Domain', 'hostname';
Check_Module 'Time::HiRes', 'gettimeofday';
if ($Opt{teragrid}) {
    Check_Module '-teragrid', 'DBI';
    Check_Module '-teragrid', 'DBD::Pg';
}

my @Notes = ();

if (not $Perl_OK or not $All_Modules_Found) {
    push @Notes, <<"EOF";
Note: Consider changing the "#!" line at the top of this script
      or invoking it with "/path/to/perl $Program_Name"
      to get a newer version of Perl and/or any missing modules.
      This should match the PERL option in your config file.

EOF
}

if (not $All_Commands_Found) {
    push @Notes, <<"EOF";
Note: Consider specifying a different value for \$PATH to find
      missing commands.  This should match the PATH option in
      your config file.

EOF
}

if (@Errors) {
    print @Errors;
    if (@Notes) {
        print "\n";
        print @Notes;
    }
    exit 1;
}
else {
    print "All preprequisites ok\n";
    if (@Notes) {
        print "\n";
        print @Notes;
    }
    exit 0;
}

########################################################################

sub Check_Perl($) {
    my($min_version) = @_;
    Vprint "Checking Perl version (need $min_version, have $]): ";
    if ($] < $min_version) {
        Vprint "too old\n";
        push @Errors, "Using Perl version $], need at least $min_version\n";
        $Perl_OK = 0;
    }
    else {
        Vprint "ok\n";
    }
} # Check_Perl

# ----------------------------------------------------------------------

sub Check_Command($) {
    my($command) = @_;    
    my $command_found = 0;
    Vprintf "%-42s", "Checking for $command command: ";
    foreach my $dir (@path) {
        if (-x "$dir/$command") {
            Vprint "found $dir/$command\n";
            $command_found = 1;
            last;
        }
    }
    if (not $command_found) {
        Vprint "not found in \$PATH=$ENV{PATH}\n";
        push @Errors, "Command $command not found\n";
        $All_Commands_Found = 0;
        return;
    }
} # Check_Command

# ----------------------------------------------------------------------

sub Check_Module(@) {
    my $teragrid = 0;
    my $module = undef;
    my $fallback_command = undef;
    my $fallback_command_found = 0;

    if (@_ and $_[0] eq '-teragrid') {
        shift;
        $teragrid = 1;
    }

    my $tg_message = $teragrid ? ' (TeraGrid only)' : '';

    if (@_) {
        $module = shift;
    }
    else {
        die "Internal error: Check_Module: No module specified\n";
    }

    if (@_) {
        $fallback_command = shift;
    }

    if (@_) {
        die "Internal error: Check_Modules: Too many arguments\n";
    }

    Vprintf "%-42s", "Checking for Perl module ${module}: ";
    eval "require $module";
    if ($@ eq '') {
        Vprint "found\n";
    }
    elsif (defined $fallback_command) {
        Vprint "\n    Module not found, trying to fall back " .
               "to $fallback_command command\n";
        foreach my $dir (@path) {
            if (-x "$dir/$fallback_command") {
                Vprint "    found $dir/$fallback_command\n";
                $fallback_command_found = 1;
                last;
            }
        }
        if ($fallback_command_found) {
            push @Warnings, "Module $module not found, falling back to " .
                            "$fallback_command command$tg_message\n";
        }
        else {
            Vprint "    Failed to find command $fallback_command\n";
            push @Errors, "Module $module and command " .
                          "$fallback_command not found$tg_message\n";
            $All_Modules_Found = 0;
        }
    }
    else {
        Vprint "not found$tg_message\n";
        push @Errors, "Module $module not found$tg_message\n";
        $All_Modules_Found = 0;
    }
} # Check_Module

# ----------------------------------------------------------------------

sub Vprint(@) {
    print @_ if $Opt{verbose};
} # Vprint

# ----------------------------------------------------------------------

sub Vprintf(@) {
    printf @_ if $Opt{verbose};
} # Vprintf

# ----------------------------------------------------------------------

sub Usage(@) {
    print @_ if @_;
    print <<"EOF";
Usage: $Program_Name [options]
    -help       Display this message and exit.
    -version    Display version number and exit.
                Note that $Program_Name doesn't really have a version 
                number; this option is included for consistency with
                other gx-map commands.
    -verbose    Verbose output
    -path ...   Specify value of \$PATH.
                Default is "/bin:/usr/bin".
    -teragrid   Check for packages required for TeraGrid functionality.
EOF
    exit 1;

} # Usage
