#!%PERL% -wT

# $Id: gx-propagate.in.teragrid,v 1.14 2005-12-21 01:07:00-08 kst Exp $
# $Source: /home/kst/gx-map-redacted/gx-propagate.in.teragrid,v $

use strict;

########################################################################
# @Copyright@
#
# Copyright (c) 2005 The Regents of the University of California. All
# rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are
# met:
#
# 1. Redistributions of source code must retain the above copyright
# notice, this list of conditions and the following disclaimer.
#
# 2. Redistributions in binary form must reproduce the above copyright
# notice, this list of conditions and the following disclaimer in the
# documentation and/or other materials provided with the distribution.
#
# 3. All advertising materials mentioning features or use of this
# software must display the following acknowledgement: This product
# includes software developed by the Grid and Cluster Computing Group
# at the San Diego Supercomputer Center and its contributors.
#
# 4. Neither the name of the Center nor the names of its contributors
# may be used to endorse or promote products derived from this software
# without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE REGENTS AND CONTRIBUTORS ``AS IS''
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO,
# THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR
# PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE REGENTS OR CONTRIBUTORS
# BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
# CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
# SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
# BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
# WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE
# OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN
# IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
#
# @Copyright@
########################################################################

########################################################################
# Developed by Keith Thompson <kst@sdsc.edu>
# TGCDB interface provided by Michael Shapiro <mshapiro@ncsa.uiuc.edu>
########################################################################

use File::Basename ();
use Getopt::Long ();

sub Usage(@);
sub Get_TGCDB_Config($);
sub Quote(@);
sub Init_Mappings();
sub Get_User_List($);
sub Get_DN_List($);
sub Add_Mapping($$$);
sub Remove_Mapping($$$);
sub Debug(@);
sub Debugf(@);

my $Install_Dir;
BEGIN {
    $Install_Dir = '%INSTALL_DIR%';
    unshift @INC, "$Install_Dir/lib";
}
use Gridmap_Utils ();

my $Program_Name = File::Basename::basename $0;
my @SAVED_ARGV = @ARGV;

my $Input_File = '$RCSfile: gx-propagate.in.teragrid,v $';
($Input_File) = ($Input_File =~ m<\ (.*),v>);

my $User_Opt = {};
my @Opts = ( $User_Opt,
             qw( help
                 version
                 debugging
                 source=s
                 add
                 remove
                 remove-dn
                 remove-user
                 dn=s
                 username=s
                 dryrun ) );
Getopt::Long::GetOptions @Opts or Usage;
Usage if $User_Opt->{help};
Usage if @ARGV;

$| = 1 if $User_Opt->{debugging};

if ($User_Opt->{version}) {
    print "$Program_Name (gx-map) $Gridmap_Utils::VERSION ($Input_File)\n";
    exit 0;
}

if (defined $User_Opt->{source}) {
    exit if $User_Opt->{source} eq 'TGCDB';
}
else {
    Usage "Missing -source option\n";
}

my @operations = ();
foreach my $op (qw(add remove remove-dn remove-user)) {
    push @operations, $op if $User_Opt->{$op};
}

my $Operation;

if (scalar @operations == 0) {
    Usage "No operation specified\n";
}
elsif (scalar @operations == 1) {
    $Operation = $operations[0];
}
else {
    Usage "Multiple operations specified\n";
}

my $message = '';
if ($Operation ne 'remove-dn' and not defined $User_Opt->{username}) {
    $message .= "Missing -username option\n";
}
if ($Operation ne 'remove-user' and not defined $User_Opt->{dn}) {
    $message .= "Missing -dn option\n";
}
Usage $message if $message ne '';

my $Config = Get_TGCDB_Config "$Install_Dir/etc/tgcdb.conf";

Debug "Configuration:\n";
foreach my $key (sort keys %$Config) {
    my $value = $Config->{$key};
    if ($key eq 'passwd') {
        $value = '[not shown]';
    }
    Debugf "    %-10s => %s\n", $key, $value;
}

if ($Operation eq 'add' or $Operation eq 'remove') {
    Debug ucfirst $Operation,
          " mapping for username $User_Opt->{username}",
          " to \"$User_Opt->{dn}\"\n";
}
elsif ($Operation eq 'remove-dn') {
    Debug "Remove mappings for DN \"$User_Opt->{dn}\"\n";
}
elsif ($Operation eq 'remove-user') {
    Debug "Remove mappings for user $User_Opt->{username}\n";
}
else {
    die "$Program_Name: Internal error, \$Operation = \"$Operation\"\n";
}

my $Mappings = undef;

#
# Database transactions produce trace output on stderr.
# Log this output to a file.
#
my $logfile = "$Install_Dir/var/tgcdb.log";
open LOG, ">>$logfile" || die "${logfile}: $!\n";
print LOG "\n", Gridmap_Utils::Time_Image time, "\n";
print LOG "$Program_Name ", Quote(@SAVED_ARGV), "\n";

open SAVE_STDERR, '>&STDERR';
print SAVE_STDERR if 0;    # to make "perl -cwT" happy
close STDERR;
open STDERR, '>&LOG';

Debug "new TGCDB\n";
my $tg;
if (not $User_Opt->{dryrun}) {
    $tg = new TGCDB (host   => $Config->{host},
                     db     => $Config->{db},
                     user   => $Config->{user},
                     passwd => $Config->{passwd});
}

Debug "\$tg->beginTransaction\n";
if (not $User_Opt->{dryrun}) {
    $tg->beginTransaction;
}
Debug "beginTransaction done\n";

if ($Operation eq 'add') {
    Add_Mapping $tg, $User_Opt->{username}, $User_Opt->{dn};
}
elsif ($Operation eq 'remove') {
    Remove_Mapping $tg, $User_Opt->{username}, $User_Opt->{dn};
}
elsif ($Operation eq 'remove-dn') {
    foreach my $username (Get_User_List $User_Opt->{dn}) {
        Remove_Mapping $tg, $username, $User_Opt->{dn};
    }
}
elsif ($Operation eq 'remove-user') {
    foreach my $dn (Get_DN_List $User_Opt->{username}) {
        Remove_Mapping $tg, $User_Opt->{username}, $dn;
    }
}
else {
    die "Internal error, \$Operation = ",
        defined $Operation ? "\"$Operation\"" : 'undef',
        "\n";
}

Debug "\$tg->commitTransaction\n";
if (not $User_Opt->{dryrun}) {
    $tg->commitTransaction;
}
Debug "commitTransaction done\n";

#
# Now put things back (probably unnecessary since we're about to exit,
# but good style).
#
close STDERR;
open STDERR, '>&SAVE_STDERR';
close LOG;

########################################################################

sub Get_TGCDB_Config($) {
    my($config_file) = @_;
    Debug "Get_TGCDB_Config $config_file\n";
    my $config = Gridmap_Utils::Read_Records '-single', $config_file;
    my @keys = sort keys %$config;
    #
    # To do: Give more detailed error messages.
    #
    if ("@keys" ne 'db host namespace passwd resource user') {
        die "Bad config file $config_file\n";
    }
    return $config;
} # Get_TGCDB_Config

# ----------------------------------------------------------------------

sub Init_Mappings() {
    if (not defined $Mappings) {
        Debug "Calling Gridmap_Utils::Gen_Mappings ",
              "-all, $Install_Dir/var/requests.log\n";
        $Mappings = Gridmap_Utils::Gen_Mappings
                        '-all', "$Install_Dir/var/requests.log";
    }
} # Init_Mappings

# ----------------------------------------------------------------------

#
# Give a DN, returns a list of usernames to which that DN is mapped.
#
sub Get_User_List($) {
    my($dn) = @_;
    $dn = "\"$dn\"";
    Debug "Get_User_List $dn\n";
    Init_Mappings;
    if (defined $Mappings->{$dn}) {
        if ($User_Opt->{debugging}) {
            my @list = sort keys %{$Mappings->{$dn}};
            Debug "Returning ( @list )\n";
        }
        return sort keys %{$Mappings->{$dn}};
    }
    else {
        Debug "Returning empty list\n";
        return ();
    }
} # Get_User_List

# ----------------------------------------------------------------------

#
# Give a username, returns a list of DNs mapped to that username.
#
sub Get_DN_List($) {
    my($username) = @_;
    Debug "Get_DN_List $username\n";
    Init_Mappings;
    #
    # This is O(N), but that's not a big deal, since it's only invoked
    # once when a remove_user request is processed (not every time a
    # grid-mapfile is generated).
    #
    my @result = ();
    foreach my $dn (keys %$Mappings) {
        if (defined $Mappings->{$dn}->{$username}) {
            $dn =~ s/^"//;
            $dn =~ s/"$//;
            push @result, $dn;
        }
    }
    Debug "Returning ( @result )\n";
    return @result;
} # Get_DN_List

# ----------------------------------------------------------------------

sub Add_Mapping($$$) {
    my($tg, $username, $dn) = @_;
    my $message =
        "\$tg->gxmap_add_dn($Config->{resource}, $username, \"$dn\")\n";
    if ($User_Opt->{dryrun}) {
        print STDERR $message;
    }
    else {
        Debug $message;
        $tg->gxmap_add_dn($Config->{resource}, $username, $dn);
    }
} # Add_Mapping

# ----------------------------------------------------------------------

sub Remove_Mapping($$$) {
    my($tg, $username, $dn) = @_;
    my $message =
        "\$tg->gxmap_remove($Config->{resource}, $username, \"$dn\")\n";
    if ($User_Opt->{dryrun}) {
        print STDERR
              "gxmap_remove_dn($Config->{resource}, $username, \"$dn\")\n";
    }
    else {
        Debug $message;
        $tg->gxmap_remove_dn($Config->{resource}, $username, $dn);
    }
} # Remove_Mapping

# ----------------------------------------------------------------------

sub Quote(@) {
    my @args = @_;
    foreach my $arg (@args) {
        if ($arg =~ / /) {
            $arg = "\"$arg\"";
        }
    }
    return join ' ', @args;
} # Quote

# ----------------------------------------------------------------------

sub Debug(@) {
    print @_ if $User_Opt->{debugging};
} # Debug

# ----------------------------------------------------------------------

sub Debugf(@) {
    printf @_ if $User_Opt->{debugging};
} # Debugf

# ----------------------------------------------------------------------

sub Usage(@) {
    print @_ if @_;
    print <<"EOF";
Usage: $Program_Name -help|-version
       $Program_Name <operation> -username <name> -dn "string"
where <operation> is one of -add, -remove, -remove-dn, -remove-user

Propagate a mapping to an implementation-defined interface, such as
a cross-site user database.

    -help           Show this message and exit.
    -version        Show version information and exit.
    -add            Add a mapping.
    -source         The source attribute of the mapping.
                    Mandatory.
                    If this is "TGCDB", it's ignored.
    -remove         Remove a mapping.
    -remove-dn      Remove all mappings for a DN.
    -remove-user    Remove all mappings for a user.
    -username name  Unix user name to map or unmap.
    -dn "string"    Distinguished name.
    -dryrun         Don't actually do the database transaction.
    -debugging      Enable debugging output.

EOF
    exit 1;
} # Usage

########################################################################

#
# The TGCDB package is included here inline to minimize the number of
# additional files that have to be installed to enable the functionality.
#
# This package was provided by Michael Shapiro <mshapiro@ncsa.uiuc.edu>.
#
{
    package TGCDB;

    use DBI;

    sub new
    {
        my($self, %attr) = @_;
        my($host, $port, $db, $user, $passwd);
        my($raiseError) = 1;
        my($printError) = 0;
        local($_);
        my($key, $value);

        while ( ($key, $value) = each(%attr))
        {
            $host       = $value if lc($key) eq "host";
            $port       = $value if lc($key) eq "port";
            $db         = $value if lc($key) eq "db";
            $user       = $value if lc($key) eq "user";
            $passwd     = $value if lc($key) eq "passwd";
            $raiseError = $value if lc($key) eq "raiseerror";
            $printError = $value if lc($key) eq "printerror";
        }
        die "host not defined"   unless (defined $host);
        die "db not defined"     unless (defined $db);
        die "user not defined"   unless (defined $user);
        die "passwd not defined" unless (defined $passwd);

        my($dsn) = "dbi:Pg:dbname=$db;host=$host";
        $dsn .= ";$port" if (defined $port);
        my($dbh) = DBI->connect($dsn, $user, $passwd,
                                  {AutoCommit=>1,
                                   RaiseError=>$raiseError,
                                   PrintError=>$printError});
        my($h) = {dbh=>$dbh, transLevel=>0};
        bless $h, $self;
    }

    sub gxmap_add_dn
    {
        my($self, $resource, $user, $dn) = @_;
        $self->_gxmap_insert_dn_tbl($resource, $user, $dn, "add");
    }

    sub gxmap_remove_dn
    {
        my($self, $resource, $user, $dn) = @_;
        $self->_gxmap_insert_dn_tbl($resource, $user, $dn, "remove");
    }

    sub _gxmap_insert_dn_tbl
    {
        my($self, $resource, $user, $dn, $action) = @_;
        my($sql) = sprintf ("select gxmap.insert_dn_tbl(%s,%s,%s,%s)",
                               $self->quote($resource, $user, $dn, $action));

        $self->dbh->do ($sql);
    }

    sub dbh
    {
        shift->{dbh};
    }

    sub quote
    {
        my($dbh) = shift->{dbh};
        map {$dbh->quote($_)} @_;
    }

    sub beginTransaction
    {
        my($self) = shift;
        $self->{dbh}->{AutoCommit} = 0 unless ($self->{transLevel} > 0);
        $self->{transLevel}++;
    }

    sub commitTransaction
    {
        my($self) = shift;
        die "not in a transaction" unless ($self->{transLevel} > 0);
        $self->{transLevel}--;
        if ($self->{transLevel} == 0)
        {
            $self->{dbh}->commit;
            $self->{dbh}->{AutoCommit} = 1;
        }
    }

    sub rollbackTransaction
    {
        my($self) = shift;
        die "not in a transaction" unless ($self->{transLevel} > 0);
        $self->{dbh}->rollback;
        $self->{dbh}->{AutoCommit} = 1;
        $self->{transLevel} = 0;
    }

    sub DESTROY
    {
        my($dbh) = shift->{dbh};
        $dbh->disconnect if ($dbh);
    }

} # end of package TGCDB
